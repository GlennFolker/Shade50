buildscript{
    repositories{
        mavenCentral()
        maven{ url 'https://www.jitpack.io/' }
    }

    dependencies{
        classpath "com.github.Anuken.Arc:arc-core:$arcVersion"
        classpath "com.github.Anuken.Arc:packer:$arcVersion"
    }
}

plugins{
    id 'java'
}

sourceSets.main{
    java.srcDirs = ['src/']
}

repositories{
    mavenCentral()
    maven{ url 'https://www.jitpack.io/' }
}

dependencies{
    implementation "com.github.Anuken.Arc:arc-core:$arcVersion"
    implementation "com.github.Anuken.Arc:g3d:$arcVersion"
    implementation "com.github.Anuken.Arc:backend-sdl:$arcVersion"
    implementation "com.github.Anuken.Arc:natives-desktop:$arcVersion"
}

ext{
    entryPoint = 'shade.Shade50'
}

import arc.packer.*

task pack{
    doFirst{
        TexturePacker.process("$rootDir/assets-raw/sprites/", "$rootDir/assets/sprites/", "sprites.aatls")
    }
}

jar{
    if(!file("$rootDir/assets/sprites/sprites.aatls").exists()) dependsOn pack

    archiveFileName = 'Shade50.jar'
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from configurations.runtimeClasspath.collect{ it.isDirectory() ? it : zipTree(it) }
    from files("$rootDir/assets")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.attributes(
        'Main-Class': entryPoint
    )
}

task run(type: JavaExec, dependsOn: [classes, configurations.runtimeClasspath]){
    if(!file("$rootDir/assets/sprites/sprites.aatls").exists()) dependsOn pack

    mainClass.set entryPoint
    workingDir "$rootDir/assets/"
    classpath sourceSets.main.runtimeClasspath
    standardInput System.in
}
